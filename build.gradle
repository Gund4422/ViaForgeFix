import me.modmuss50.mpp.ReleaseType

buildscript {
    repositories {
        gradlePluginPortal()
        maven { url = "https://maven.minecraftforge.net/" }
        maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
    }
    dependencies {
        classpath "com.gradleup.shadow:shadow-gradle-plugin:9.3.1"
        classpath "net.minecraftforge.gradle:ForgeGradle:[6.0.36,6.2)"
        classpath "org.spongepowered:mixingradle:0.7-SNAPSHOT"
        classpath "xyz.wagyourtail.jvmdowngrader:gradle-plugin:1.3.6"
        classpath "me.modmuss50.mod-publish-plugin:me.modmuss50.mod-publish-plugin.gradle.plugin:1.1.0"
        classpath "net.raphimc.class-token-replacer:net.raphimc.class-token-replacer.gradle.plugin:1.1.7"
    }
}

allprojects {
    apply plugin: "java-library"
    apply plugin: "com.gradleup.shadow"
    apply plugin: "xyz.wagyourtail.jvmdowngrader"

    java {
        toolchain.languageVersion = JavaLanguageVersion.of(8)
    }

    compileJava.setSourceCompatibility("1.8")
    compileJava.setTargetCompatibility("1.8")

    configurations {
        library
        implementation.extendsFrom(library)
    }

    repositories {
        maven { url = "https://maven.minecraftforge.net/" }
        maven { url = "https://repo.spongepowered.org/repository/maven-public" }
        maven {
            url = "https://repo.viaversion.com"
            metadataSources {
                mavenPom()
                artifact()
            }
        }
    }

    dependencies {
        library "com.viaversion:viaversion-common:5.7.2-SNAPSHOT"
        library "com.viaversion:viabackwards-common:5.7.2-SNAPSHOT"
        library "com.viaversion:viarewind-common:4.0.14"
        library "com.viaversion:viaaprilfools-common:4.0.8"
        library ("net.raphimc:ViaLegacy:3.0.13") {
            exclude group: "com.google.code.gson", module: "gson"
        }
    }
}

subprojects {
    apply plugin: "net.minecraftforge.gradle"
    apply plugin: "org.spongepowered.mixin"
    apply plugin: "me.modmuss50.mod-publish-plugin"

    base {
        group = project.maven_group
        archivesName = project.name
        version = project.maven_version
    }

    // Fixed to 1.8.9
    def mcVersion = "1.8.9"
    compileJava.options.encoding = "UTF-8"

    dependencies {
        library project(":") 
    }

    minecraft {
        // 1.8.9 uses MCP mappings
        runs {
            client {
                workingDirectory project.file("run")
                property "forge.logging.markers", "REGISTRIES"
                property "forge.logging.console.level", "debug"
                property "mixin.debug.export", "true"
                property "mixin.hotSwap", "true"
                property "fml.coreMods.load", "com.viaversion.viaforge.mixin.MixinLoader"
                args "-mixin.config=" + "mixins.viaforge.json"

                mods {
                    "${project.name}" {
                        source sourceSets.main
                    }
                }
            }
        }
    }

    sourceSets.main.resources {
        srcDir "src/generated/resources"
    }

    dependencies {
        minecraft "net.minecraftforge:forge:${forge_version}"
        library "org.slf4j:slf4j-api:2.0.17"
    }

    mixin {
        add sourceSets.main, "mixins.viaforge.refmap.json"
    }

    reobf {
        shadowJar {}
    }

    tasks {
        processResources {
            filesMatching("mcmod.info") {
                expand(
                    "version": project.version,
                    "description": project.description
                )
            }
        }
    }

    jar {
        manifest.attributes(
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "TweakOrder": "0",
            "FMLCorePluginContainsFMLMod": "true",
            "FMLCorePlugin": "com.viaversion.viaforge.mixin.MixinLoader",
            "MixinConfigs": "mixins.viaforge.json",
            "ForceLoadAsMod": "true"
        )
        enabled = false
    }

    shadowJar {
        destinationDirectory = temporaryDir
        configurations = [project.configurations.library]
        duplicatesStrategy DuplicatesStrategy.EXCLUDE
        exclude("META-INF/maven/**")
        exclude("META-INF/versions/**")
        relocate("org.slf4j", "com.viaversion.viaforge.libs.slf4j")
    }

    downgradeJar {
        inputFile = shadowJar.archiveFile
        destinationDirectory = temporaryDir
        logLevel = "FATAL"
    }

    shadeDowngradedApi {
        archiveFileName = jar.archiveFileName
        logLevel = "FATAL"
    }

    jar.dependsOn("shadowJar")
    jar.dependsOn("shadeDowngradedApi")

    publishMods {
        file = tasks.shadeDowngradedApi.archiveFile
        changelog = rootProject.file("CHANGELOG").text
        version = project.version.toString() + "+" + mcVersion
        displayName = version
        modLoaders.add("forge")
        type = ReleaseType.STABLE

        curseforge {
            projectId = "418933"
            javaVersions.add(JavaVersion.VERSION_1_8)
            minecraftVersions.add(mcVersion)
        }
        modrinth {
            projectId = "Z6se2s8f"
            minecraftVersions.add(mcVersion)
        }
    }

    tasks.publishMods.dependsOn tasks.build
}

dependencies {
    compileOnly "io.netty:netty-all:4.2.2.Final"
    compileOnly "org.slf4j:slf4j-api:2.0.17"
}

apply plugin: "net.raphimc.class-token-replacer"

sourceSets {
    main {
        classTokenReplacer {
            property("\${version}", project.maven_version)
        }
    }
}
